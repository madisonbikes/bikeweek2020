plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.4.0'
    id 'org.jetbrains.kotlin.kapt' version '1.4.0'
}

group 'org.madisonbikes.bikeweek'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url = uri("http://dl.bintray.com/steppschuh/Markdown-Generator")
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "com.squareup.okio:okio:2.2.2"
    implementation "com.squareup.okhttp3:okhttp:3.14.1"
    implementation "net.sf.supercsv:super-csv:2.4.0"
    implementation "net.steppschuh.markdowngenerator:markdowngenerator:1.3.1.1"
    implementation "net.sourceforge.argparse4j:argparse4j:0.8.1"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task("downloadEvents", type: Exec) {
    doFirst {
        mkdir 'data'
    }
    commandLine 'wget',
            'https://docs.google.com/spreadsheets/d/19ils5BDZpkBe00H8wsQ2Cj1ANb9ib27iLVbho0k7aeg/export?format=csv&gid=0',
            '-O',
            'data/events.csv'
}

task("generate", type: JavaExec, dependsOn: downloadEvents) {
    classpath sourceSets.main.runtimeClasspath
    main 'org.madisonbikes.bikeweek.ProcessEventsTask'
    args 'data/events.csv', 'data'
}

task calendar(type: Exec, dependsOn: generate) {
    doFirst {
        mkdir 'data/html'
    }

    commandLine 'pandoc', '+RTS', '-K512m', '-RTS', 'data/events.md',
            '--to', 'html4', '--from', 'markdown+autolink_bare_uris+ascii_identifiers+smart',
            '--template', 'pandoc.template',
            '--output', 'data/html/calendar.html', '--section-divs', '--standalone',
            '--table-of-contents', '--toc-depth', '2', '--metadata=title=Madison Bike Week Calendar of Events'
}

task upload(type: Exec) {
    commandLine 'curl', '--insecure',
            '--user', "${madisonBikesWebUsername}:${madisonBikesWebPassword}",
            '-T', 'data/html/calendar.html',
            'sftp://ssh.madisonbikes.org/~/madisonbikes.org/wp-content/bikeweek2020-calendar-snippet.html',
            '-v'

}